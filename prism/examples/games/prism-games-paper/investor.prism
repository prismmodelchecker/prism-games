// Futures market investor (McIver and Morgan 2007)
//
// Example with two investors
//
// gxn/dxp

smg

// Player info
player investor1 [invest1], [noinvest1], [cashin1] endplayer
player investor2 [invest2], [noinvest2], [cashin2] endplayer
player env sched, [bar1], [nobar], [bar2], [month], [done] endplayer

// Scheduler use to synchronise system transitions
const double pterm = 0.01; 
module sched
	
	term: [0..1];
	t: [0..3];
	m1 : [0..2];
	m2 : [0..2];

	// scheduling investors
	[] t=0 & m1=0 & i1!=2 & term=0 -> (1-pterm):(t'=1) + pterm:(term'=1);
	[] t=0 & m2=0 & i2!=2 & term=0 -> (1-pterm):(t'=2) + pterm:(term'=1);
	[] t=0 & (m1!=0 | i1=2) & (m2!=0 | i2=2) & term=0 -> (t'=3);

	// At the start of the month, investor makes decision
	[noinvest1] (m1=0) & t=1 -> (m1'=1) & (t'=0); 
	[invest1] (m1=0) & t=1 -> (m1'=1) & (t'=0);

	[noinvest2] (m2=0) & t=2 -> (m2'=1) & (t'=0); 
	[invest2] (m2=0) & t=2 -> (m2'=1) & (t'=0);

	// Then, decision is made whether to bar or not
	[nobar] (m1=1 | i1=2) & (m2=1 | i2=2) & t=3 & !(i1=2 & i2=2) -> (m1'=2) & (m2'=2);
	[bar1] m1=1 & t=3 -> (m1'=2) & (m2'=2);
	[bar2] m2=1 & t=3 -> (m1'=2) & (m2'=2);


	// Then, market changes
	[month] (m1=2) & (m2=2) -> (m1'=0) & (m2'=0) & (t'=0);


	// Once investor has cashed in shares nothing changes
	[cashin1] m1=0 & t=1 -> (m1'=0) & (t'=0);
	[cashin2] m2=0 & t=2 -> (m2'=0) & (t'=0);

	[done] (m1=0 & m2=0 & t=3) | term=1-> true;

endmodule

// Investor
module investor1
	
	// State: 0 = no reservation, 1 = made reservation, 2 = finished
	i1 : [0..2];

	// Decide whether to do nothing or make reservation
	// (if currently not reserving or was barred last time)
	[noinvest1] (i1=0 | i1=1 & b1=1) -> (i1'=0);
	[invest1] (i1=0 | i1=1 & b1=1) -> (i1'=1);
	// Cash in shares (if not barred)
	[cashin1] i1=1 & b1=0 -> (i1'=2);
	// Finished
	[done] (i1=2) | term=1-> true;

endmodule

module investor2 = investor1 
[
	i1=i2, 
	b1=b2,
	noinvest1=noinvest2,
	invest1=invest2,
	cashin1=cashin2
] 
endmodule

// Bar on the investor
module barred
	
	// State: 0 = not barred, 1 = barred
	// (initially cannot bar)
	b1 : [0..1] init 1;
	b2 : [0..1] init 1;

	// Bar or not bar (cannot if do so last month)
	[nobar] true -> (b1'=0) & (b2'=0);
	[bar1] b1=0 -> (b1'=1) & (b2'=0);
	[bar2] b2=0 -> (b2'=1) & (b1'=0);

endmodule

// Value of the shares
const vmax=10;
const vinit;
module value
	
	v : [0..vmax] init vinit;

	[month] true -> p/10 : (v'=min(v+1,c,vmax)) + (1-p/10) : (v'=min(max(v-1,0),c));

	// Note that, because the shares and the cap are updated simultaneously,
	// v can exceed c temporarily (but by at most 1).
	// We leave this as-is for compatibility with the original model

endmodule

// Probability of shares going up/down
const int pmax = 10;
module probability
	
	// Probability is p/pmax and initially the probability is approx 1/2
	p : [0..pmax] init floor(pmax/2);

	[month] (v<5) -> 2/3 : (p'=min(p+1,pmax)) + 1/3 : (p'=max(p-1,0));
	[month] (v=5) -> 1/2 : (p'=min(p+1,pmax)) + 1/2 : (p'=max(p-1,0));
	[month] (v>5) -> 1/3 : (p'=min(p+1,pmax)) + 2/3 : (p'=max(p-1,0));

endmodule

// Cap on the value of the shares
const int cmax = vmax;
module cap
	
	c : [0..cmax] init cmax;

	[month] true -> 1/2 : (c'=max(c-1,0)) + 1/2 : (c'=c); // probability 1/2 the cap decreases

endmodule

// Labels
label "finished" = i1=2 & i2=2;
label "finished1" = i1=2;
label "finished2" = i2=2;

// Reward: one-off collection of shares value at the end
// for the first investor
rewards "profit1"
	//i1=1 & b1=0 & m1=0  : v;
	[cashin1] i1=1 : v;
endrewards


// Reward: one-off collection of shares value at the end
// for the second investor
rewards "profit2"
	//i2=1 & b2=0 & m2=0 : v;
	[cashin2] i2=1 : v;
endrewards

// Reward: one-off collection of shares value at the end
// for both investors
rewards "profit12"
	[cashin1] i1=1 : v;
	[cashin2] i2=1 : v;
endrewards
